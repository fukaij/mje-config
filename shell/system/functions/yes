# ZSH function file
# Repeatedly print a string
# Usage: yes [-l <count>] [-s <seconds>] [<options>] [<text>]
#   -l <count>     - Output only count times
#   -s <seconds>   - Sleep for given number of seconds between each printing
#   options & text - As for the built in echo, except text defaults to 'yes'
#
# $Id: yes,v 1.1 2001/06/14 16:17:15 mebourne Exp $

integer count=-1 delay=0

# Process our options
while [[ $argv[1] == -[ls] ]]
do
  case $argv[1] in
    -l) count=$argv[2]
        shift 2;;
    -s) delay=$argv[2]
        shift 2;;
  esac
done

# Default text to 'yes' if no parameters passed
local -a args
args=($argv)
if (( ! $#args ))
then
  args=('yes')
fi

# Output as required
while (( count ))
do
  echo "$args[@]"
  (( delay )) && sleep $delay
  (( count > 0 && count-- ))
done
