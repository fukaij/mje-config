# ZSH function file
# Wrapper for dbx to enter VT52 terminal mode. Makes cursor keys work with appropriate settings
# Usage: dbx <arguments>
#

# Don't try anything fancy unless in an xterm
if [[ $TERM == *xterm* ]]
then
  # The first code switches from vt100->vt52, the second back again.
  # The second will work even in vt100
  local setvt52=$'\e[?2l' resetvt52=$'\e<'

  # If the prompt package is in use then modify the reset string in case user
  # suspends dbx or something
  typeset -g prompt_reset="$prompt_reset$resetvt52"

  # Run dbx in vt52 mode, and then switch back. DBXVT* are used in the dbxrc file
  # to set the dbx prompt
  echo -n $setvt52
  DBXVTRESET="$resetvt52" DBXVT52="$setvt52" command dbx "$argv[@]"
  echo -n $resetvt52

  # If we are the only ones to set prompt_reset now we can remove it
  if [[ $prompt_reset == "$resetvt52" ]]
  then
    unset prompt_reset
  fi
else
  command dbx "$argv[@]"
fi
