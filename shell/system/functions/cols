#eval compdef -n _parse_opts cols
# ZSH function file
# Filter stdin for just the given columns
#
# $Id: cols,v 1.3 2001/07/31 17:17:18 mebourne Exp $

emulate -L zsh
setopt extended_glob

local -a args
args=($argv[*])
local -A opts
opts[delimiter]=$IFS

parse_opts args opts - 1 <<'EOF' || return 1
cols [options] <column-number> ...
  -d, --delimiter		Field delimiter. As for IFS and defaults to whitespace
				# delimiter | d = string
  -h, --help			Provide this help
				# help | h
EOF

# Check all column numbers really are numbers
local -a columns
columns=($args)
for column in $columns
do
  if [[ $column != [0-9]## ]]
  then
    echo "Invalid column number $column" 1>&2
    return 1
  fi
done

# Working storage
local -a input output

# Read a line from the input using given word separators. -r for raw mode
while IFS=$opts[delimiter] read -r -A input
do
  output=()

  # Iterate over the list of columns retrieving them as required
  for column in $columns
  do
    output=($output $input[$column])
  done

  # Write new output line
  echo $output
done

return 0
