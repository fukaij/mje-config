#eval compdef -n _parse_opts cols
# ZSH function file
# Filter stdin for just the given columns
#
# $Id: cols,v 1.4 2001/08/01 19:37:11 mebourne Exp $

emulate -L zsh
setopt extended_glob

local -a args
args=($argv[*])
local -A opts
opts[delimiter]=$IFS

local -a columns

parse_opts args opts <<'EOF' || return 1
Description:
Filter each line of input and return just the given columns

Usage:
cols [options] <column-number> ...

Options:
  -d, --delimiter		Field delimiter. As for IFS and defaults to whitespace
				# delimiter | d = string
  -h, --help			Provide this help
				# help | h

Arguments:
  <column-number> ...		The column numbers to return, where 1 is the left-most column.
				Can use -ve numbers, eg. -1 is rightmost column
				# [columns] = [1,*] = integer
EOF

# Working storage
local -a input output

# Read a line from the input using given word separators. -r for raw mode
while IFS=$opts[delimiter] read -r -A input
do
  output=()

  # Iterate over the list of columns retrieving them as required
  for column in $columns
  do
    output=($output $input[$column])
  done

  # Write new output line
  echo $output
done

return 0
