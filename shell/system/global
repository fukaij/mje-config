# ZSH configuration file
# Executed for all shells via zshenv
# Generic system-wide setup for ALL shells. This is executed before anything else
# Written by Martin Ebourne
#
# $Id: global,v 1.11 2002/04/23 10:24:42 mebourne Exp $

# Make various path like variables keep unique values only. Need to create any that aren't built
# into the shell. Most of them are also exported
typeset -Ugx  CDPATH
typeset -Ugx  FIGNORE
typeset -Ug   FPATH
typeset -UgxT LD_LIBRARY_PATH="$LD_LIBRARY_PATH" ld_library_path
typeset -UgxT INFOPATH="$INFOPATH" infopath
typeset -Ugx  MAILPATH
typeset -Ugx  MANPATH
typeset -Ug   MODULE_PATH
typeset -Ugx  PATH
typeset -UgxT PERLLIB="$PERLLIB" perllib
typeset -Ugx  WATCH

# Emacs makes use of these for mode selection
export ZCONFIGDIR ZUSERCONFIGDIR

# Prompt used for execution tracing (set -x). Includes timestamp & seconds for
# profiling. Set here because it is of use in scripts, and in profiling the shell
# configuration
PROMPT4=': %N:%i %D{%H:%M:%S} {$SECONDS} ; '

# Check for properties of our term type. XTerm & colour are the interesting ones
case $TERM in
  *xterm*colo(u|)r* | *colo(u|)r*xterm*)
    COLOUR=1
    XTERM=1
    ;;

  *xterm*)
    # Generic XTerm may not support colour, but should ignore unrecognised
    # codes. Difficult to tell otherwise if colour will work. Can override
    # with environment if required
    #[[ -z $COLOUR ]] && COLOUR=0
    [[ -z $COLOUR ]] && COLOUR=1
    XTERM=1
    ;;

  rxvt|dtterm|vs100)
    COLOUR=0
    XTERM=1
    ;;

  *)
    COLOUR=0
    XTERM=0
    ;;
esac

# If ~/.zsh exists as a directory, and a ZDOTDIR has not been specified, then
# default it to this
if [[ -z $ZDOTDIR && -d $HOME/.zsh ]]
then
  export ZDOTDIR="$HOME/.zsh"
  [[ -n $ZCONFIGDEBUG ]] && echo "ZDOTDIR set to $ZDOTDIR"
fi

# Enable functions early & for everyone
config_if_present system/loadfuncs

# Set up want/go. Use ZDOTDIR if set, else HOME. This should make the system
# work over machines
semaphore_base=${ZDOTDIR:-$HOME}/.tmp.semaphore.
