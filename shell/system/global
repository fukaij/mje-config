# ZSH configuration file
# Executed for all shells via zshenv
# Generic system-wide setup for ALL shells. This is executed before anything else
# Written by Martin Ebourne
#
# $Id: global,v 1.3 2001/04/20 12:06:32 mebourne Exp $

# Make various path like variables keep unique values only. Need to create any that aren't built
# into the shell. Most of them are also exported
typeset -Ugx  CDPATH
typeset -Ugx  FIGNORE
typeset -U    FPATH
typeset -UgxT INFOPATH="$INFOPATH" infopath
typeset -Ugx  MAILPATH
typeset -Ugx  MANPATH
typeset -U    MODULE_PATH
typeset -Ugx  PATH
typeset -Ugx  WATCH

# Prompt used for execution tracing (set -x). Includes timestamp & seconds for
# profiling. Set here because it is of use in scripts, and in profiling the shell
# configuration
PROMPT4=': %N:%i %D{%H:%M:%S} {$SECONDS} ; '

# Check for properties of our term type. XTerm & colour are the interesting ones
COLOUR=0
XTERM=0
case $TERM in
  *xterm*colo(u|)r* | *colo(u|)r*xterm*)
    COLOUR=1
    XTERM=1
    ;;

  *xterm*)
    # Generic XTerm may not support colour, but should ignore unrecognised codes.
    # Difficult to tell otherwise if colour will work. Disable here if required
    COLOUR=1
    XTERM=1
    ;;

  rxvt|dtterm|vs100)
    COLOUR=0
    XTERM=1
    ;;
esac

# Enable functions early & for everyone. Ha! Should be here but zsh later goes and
# resets the stupid path. Doh! Have to do it at the start of login AND interactive
# instead
#config_if_present system/loadfuncs
