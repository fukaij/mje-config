# ZSH configuration file
# Executed for interactive shells via zshrc
# User's own setup for interactive shells. Run after all other setup has been done
# Written by Martin Ebourne
#
# $Id: interactive,v 1.3 2001/03/01 19:20:40 mebourne Exp $

# Enable incremental history saving
package history

# Get the normal XPG4 standards commands rather than the strange sun ones
# Can't just change PATH since that breaks scripts
if [ -d /usr/xpg4/bin ]
then
  local xpgcmd
  for xpgcmd in /usr/xpg4/bin/*
  do
    local cmd=${xpgcmd:t}

    # Check path of command, ignoring alias definitions etc
    case $(whence -p $cmd) in
      /bin/* |\
      /usr/bin/* |\
      /usr/ucb/* |\
      /usr/ccs/bin/*)
        # Only do anything if command is not already overridden by a useful implementation in
        # the path. (eg. one in local/bin or gnu/bin would be assumed to be ok)
        case ${$(whence -w $cmd)[(w)2]} in
	  alias)
	    # Edit already existing aliases so we don't lose them
            alias $cmd="${$(whence $cmd)//$cmd/$xpgcmd}"
	    ;;
	  command | hashed)
	    # If its a command then add a new simple alias
	    alias $cmd=$xpgcmd
	    ;;
	  builtin | function | reserved | none)
	    # If its a built-in or function or something then don't touch
	    ;;
	  *)
	    # Shouldn't get here
	    ;;
	esac
    esac
  done
fi

# Prompt stuff

typeset -gA pcol
pcol[user]

setprompt() {
  local var=$1
  shift

  local sf=$'%{\e[38;5;' ef=$'m%}'
  local value

  while (( $#@ > 1 ))
  do
    local colour=$1 text=$2
    shift 2
    value="$value$sf$colour$ef$text"
  done

  if (( $#@ ))
  then
    local colour=$1
    value="$value$sf$colour$ef"
  fi

  eval $var=\$$var\$value
}

PROMPT=
setprompt PROMPT 214 '$USERNAME' 15 '@' 10 '%m'
if (( SHLVL > 2 ))
then
  setprompt PROMPT 15 "#$[SHLVL-2]"
fi
if [[ -n "$CLEARCASE_TRIM_VIEW" ]]
then
  setprompt PROMPT 15 '<' 11 '$CLEARCASE_TRIM_VIEW' 15 '>'
else
  setprompt PROMPT 15 ':'
fi
setprompt PROMPT 14 '%3~' "15;01" '%# ' "15;00"

RPROMPT=
setprompt RPROMPT 247 '%*' 15

#preexec() {
#  echo "\e[38;5;15m"
#}
