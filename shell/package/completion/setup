# ZSH configuration file
# Executed when the completion package is included
# Setup for completion package
# Written by Martin Ebourne
#
# $Id: setup,v 1.4 2001/05/09 15:59:13 mebourne Exp $

add_functions_directory package/completion/functions

# Load the completion module. -C speeds things up a lot by skipping audit & zcompdump file
# regeneration check. Need to rely on user to be sensible about these things
autoload -U compinit
compinit -C


# These are global settings

# The default sequence to try for completion
zstyle ':completion:*' completer _expand _complete _ignored _approximate _prefix

# Make completion case insensitive
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

# Enable colours as for ls
if (( COLOUR ))
then
  zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
fi

# Reduce space used for completion listing
zstyle ':completion:*:default' list-packed true

# List each group of completions under separate headings (works with formatting below)
zstyle ':completion:*' group-name ''


# These are modifications to specific completers

# Set expand to do file wildcards and variable substitutions
zstyle ':completion:*:expand:*' glob 1
zstyle ':completion:*:expand:*' substitute 1

# Force expansion to insert the expected expansion rather than menu-complete
# on a load of other possibilities
zstyle ':completion:*:expand:*' tag-order all-expansions expansions original

# Prevent listing of original along with corrections
zstyle ':completion:*:correct:*' tag-order corrections original

# Allow one error for every five or part thereof characters typed in approximate completer
zstyle -e ':completion:*:approximate:*' max-errors \
  'reply=( $(( ($#PREFIX+$#SUFFIX)/5 + 1 )) numeric )'

# In the case of using prefix to obtain a completion then add a space in to
# separate the real bit from the discarded stuff
zstyle ':completion:*:prefix:*' add-space true


# These are modifications to particular commands

# When completing for 'cd' try local directories before attempting to use cdpath
zstyle ':completion:*:complete:cd:*' tag-order '! path-directories'
# Same for commands. Doesn't work
zstyle ':completion:*:complete:-command-:*' tag-order '! path-directories'

# Try indexes before parameters in subscripts
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters


# These are modifications to particular result sets

# Ignore completion functions (until the _ignored completer)
zstyle ':completion:*:*:*:functions' ignored-patterns '_*'


# These give the description format strings. If the prompt package is loaded then use it to
# get colour versions
if package -e prompt
then
  local temp
  
  # Describe the group of completions
  set_prompt temp {black} {bg_cyan} '%d:' {normal}
  zstyle -e ':completion:*:*:*:descriptions' format reply=\"$temp\"
  
  # Give info on errors when corrected
  set_prompt temp {black} {bg_cyan} '%d (errors: %e):' {normal}
  zstyle -e ':completion:*:*:*:corrections' format reply=\"$temp\"
  
  # Notify if no matches
  set_prompt temp {black} {bg_red} 'No matches for: %d' {normal}
  zstyle -e ':completion:*:*:*:warnings' format reply=\"$temp\"
  
  # Displaying miscellaneous messages
  set_prompt temp {black} {bg_magenta} '%d' {normal}
  zstyle -e ':completion:*:*:*:messages' format reply=\"$temp\"

  # More-style prompt when scrolling completions
  zstyle ':completion:*:default' list-prompt '%S<completions> line %l match %m (%p)%s'
else
  # These are the non-colour versions
  zstyle ':completion:*:*:*:descriptions' format '%d:'
  zstyle ':completion:*:*:*:corrections' format '%d (errors: %e):'
  zstyle ':completion:*:*:*:warnings' format 'No matches for: %d'
  zstyle ':completion:*:*:*:messages' format '%d'
  zstyle ':completion:*:default' list-prompt '%S<completions> line %l match %m (%p)%s'
fi


# Set up so future added function directories are processed for completions, and ensure all
# previously added function dirs in the config system are re-processed
add_to_hook _add_functions_hook '_completion_add_functions "$argv[@]"'
_completion_add_directories ${(M)fpath:#$ZCONFIGDIR/*}
