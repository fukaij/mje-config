# ZSH configuration file
# Executed when the completion package is included
# Setup for completion package
# Written by Martin Ebourne
#
# $Id: setup,v 1.2 2001/04/20 11:31:16 mebourne Exp $

add_functions_directory package/completion/functions

# Load the completion module
autoload -U compinit
compinit


# These are global settings

# The default sequence to try for completion
zstyle ':completion:*' completer _expand _complete _ignored _approximate _prefix

# Make completion case insensitive
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

# Enable colours as for ls
if (( COLOUR ))
then
  zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
fi

# Reduce space used for completion listing
zstyle ':completion:*:default' list-packed true


# These are modifications to specific completers

# Set expand to do file wildcards and variable substitutions
zstyle ':completion:*:expand:*' glob 1
zstyle ':completion:*:expand:*' substitute 1

# Force expansion to insert the expected expansion rather than menu-complete
# on a load of other possibilities
zstyle ':completion:*:expand:*' tag-order all-expansions expansions original

# Prevent listing of original along with corrections
zstyle ':completion:*:correct:*' tag-order corrections original

# In the case of using prefix to obtain a completion then add a space in to
# separate the real bit from the discarded stuff
zstyle ':completion:*:prefix:*' add-space true


# These are modifications to particular commands

# When completing for 'cd' try local directories before attempting to use cdpath
zstyle ':completion:*:complete:cd:*' tag-order '! path-directories'
# Same for commands. Doesn't work
zstyle ':completion:*:complete:-command-:*' tag-order '! path-directories'


# Set up so future added function directories are processed for completions, and ensure all
# previously added function dirs in the config system are re-processed
add_to_hook _add_functions_hook '_completion_add_functions "$argv[@]"'
_completion_add_directories ${(M)fpath:#$ZCONFIGDIR/*}
