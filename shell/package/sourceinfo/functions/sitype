# ZSH function file
# Written by Martin Ebourne
# Part of the SourceInfo package
# Display type information
# Usage: sitype [-r] [-d <dir-name>] <type-name>
#	 -d	Prefer files within directory if duplicates found (partial name ok)
#	 -r	Display full type information recursively
#
# $Id: sitype,v 1.4 2001/07/13 17:49:12 mebourne Exp $

[[ -z $sourceinfo_typesdb ]] && { echo "sourceinfo_typesdb not set" ; return 1 }

local -a options

# Extract the -d <dir-name> options
zparseopts -D -E d:=options

# If no -d options then add all the default ones instead
if (( !$#options ))
then
  local dir
  for dir in $sourceinfo_preferred_dirs
  do
    options=($options -d $dir)
  done
fi

# Extract remaining options
zparseopts -D -a options r

# Give help if error with parameters
if [[ $ARGC -ne 1 || $argv[1] == -* ]]
then
  {
    echo "Syntax: sitype [-r] [-d <dir-name>] <type-name>"
    echo "  -d    Prefer files within directory if duplicates found (partial name ok)"
    echo "  -r    Display full type information recursively"
  } 1>&2
  return 1
fi

typeInfo.pl $options $sourceinfo_typesdb $argv[1]
