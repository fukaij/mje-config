#eval compdef -n _parse_opts myview
# ZSH function file
# Part of the clearcase package
# Set view to that matching username
#

_clearcase_getviews

local -A opts
parse_opts - opts -- "$argv[@]" <<'EOF' || return 1
Description:
Set view to that matching username.

Usage:
myview [options] [<username>]

Options:
  -h, --help			Provide this help
				# --help | -h * excludes=*
  -u <username>, --user=<username>
				The username to match against (defaults to
				current user)
				# --username | -u : users

Arguments:
  [<tag>]			The tag to match against (defaults to none)
				# [tag] = [0,1] tag : ? string
##
#type users string users _users
EOF

local -aU views
views=(${(M)_clearcase_views:#$opts[tag]})
if (( !$#views ))
then
  views=(${(M)_clearcase_views:#*${opts[username]:-$USER}*$opts[tag]*}
         ${(M)_clearcase_views:#*$opts[tag]*${opts[username]:-$USER}*})
fi

if (( $#views != 1 ))
then
  echo "Cannot determine suitable view on this machine. Choices are:" 1>&2
  print -c $views 1>&2
  return 1
fi

echo "Setting view to $views"
cleartool setview $views
