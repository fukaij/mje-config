#eval compdef -n _parse_opts myview
# ZSH function file
# Part of the clearcase package
# Set view to that matching username
#
# $Id: myview,v 1.6 2002/03/19 12:00:32 mebourne Exp $

_clearcase_getviews

local -A opts
parse_opts - opts -- "$argv[@]" <<'EOF' || return 1
Description:
Set view to that matching username (defaults to current user).

Usage:
myview [options] [<username>]

Options:
  -h, --help			Provide this help
				# --help | -h * excludes=*

Arguments:
  [<username>]			The username to match
				# [username] = [0,1] username : ? users
##
#type users string users _users
EOF

local -a views
views=(${(M)_clearcase_views:#*${opts[username]:-$USER}*})

if (( $#views != 1 ))
then
  echo "Cannot determine suitable view on this machine. Choices are:" 1>&2
  print -c $views 1>&2
  return 1
fi

echo "Setting view to $views"
cleartool setview $views
