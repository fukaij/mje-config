# ZSH function file
# Part of the prompt package
# Hook for preexec function
# Usage: _prompt_precmd
#
# $Id: _prompt_preexec,v 1.5 2001/03/08 17:52:20 mebourne Exp $

local command="$1"

# Calculate the number of rows used by the command line (including left prompt)
local rows
(( rows = 1 + ( ${#${(%%)${PROMPT//[%]\{[^%]#%\}/}}} + $#command ) / COLUMNS ))

# Move up the required number of rows to get to the start of the command line
repeat $rows
do
  echotc up
done


## Redraw the command line

# First the left prompt
echo -n "${(%%)PROMPT}"

# Then the command in bold
echo -n "\e[$prompt_colours[bold]m"
echo -nE "$command"
echo -n "\e[$prompt_colours[normal]m"

# Then the padding to right justify the right prompt
echo -n "$_prompt_gap[$[ ${#${(%%)${PROMPT//[%]\{[^%]#%\}/}}} + $#command
                         + ${#${(%%)${prompt_right//[%]\{[^%]#%\}/}}} - COLUMNS * rows ],-1]"

# Then the right prompt
echo "${(%%)prompt_right}"
