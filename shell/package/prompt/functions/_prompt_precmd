# ZSH function file
# Part of the prompt package
# Hook for precmd function to handle prompt formatting and displaying
# Usage: _prompt_precmd
#
# $Id: _prompt_precmd,v 1.6 2001/03/19 13:50:51 mebourne Exp $

# Null out old strings to get base size
_prompt_justify=""
_prompt_pwd=""

# Get padding required for base string
_prompt_justify=$_prompt_gap[${#${(%%)${prompt_above//[%]\{[^%]#%\}/}}[(f)1]},-1]

# Generate the current directory, losing bits off the front until it fits
_prompt_pwd=$(print -D - $PWD)
while (( $#_prompt_pwd > 1 && $#_prompt_pwd > $#_prompt_justify ))
do
  _prompt_pwd=${_prompt_pwd#...}
  case $_prompt_pwd in
    /)		;;
    /*/*)	_prompt_pwd=".../${_prompt_pwd#/*/}";;
    [^/]*/*)	_prompt_pwd=".../${_prompt_pwd#*/}";;
    *)		_prompt_pwd=".";;
  esac
done

# Get the padding required including the current directory
_prompt_justify="${${_prompt_justify[$#_prompt_pwd,-1]}% }"

# Display the prompt
echotc cr
print -nP - $prompt_above

# May need a newline depending on terminal
if [[ $(echotc am 2>/dev/null) != yes || $(echotc xn 2>/dev/null) == yes ]]
then
  echo
fi
