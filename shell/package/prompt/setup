# ZSH configuration file
# Executed when the prompt package is included
# Setup for prompt package
# Written by Martin Ebourne
#
# $Id: setup,v 1.12 2001/08/15 16:15:03 mebourne Exp $

add_functions_directory package/prompt/functions

# Declare work variables
typeset -g _prompt_pwd _prompt_gap _prompt_justify
typeset -g _prompt_wrapmode=0 _prompt_clearline _prompt_columns=$COLUMNS

# Cache terminal handling of line wrapping
if [[ $(echotc am 2>/dev/null) != yes || $(echotc xn 2>/dev/null) == yes ]]
then
  _prompt_wrapmode=1
fi

# Cache termcap code for clearing line
_prompt_clearline=$(echotc ce)

# Register our hooks
add_to_hook precmd _prompt_precmd
add_to_hook preexec '_prompt_preexec "$1"'
add_to_hook TRAPWINCH _prompt_winch

# Fake resize event to force setup
_prompt_winch

# Remap ZLE clear-screen widget (Ctrl-L) to a modified version
zle -N clear-screen _prompt_clear_screen


## Now we configure the default prompts

typeset -g prompt_above prompt_right

# Configure the above line prompt
colour_setvar prompt_above
colour_addvar {dark_red} '%$COLUMNS>>%n' {grey} '@' {dark_green} '%m'
if (( SHLVL > 2 ))
then
  colour_addvar {grey} '  #$[SHLVL-2]'
fi
#colour_addvar {dark_red} '%(?..  [%?${signals[$?-127]:+ $signals[$?-127]}])'
colour_addvar {dark_magenta} '${_sql_prompt:+  $_sql_prompt}'
colour_addvar {dark_yellow} '${CLEARCASE_VIEW:+  $CLEARCASE_VIEW}'
colour_addvar {dark_cyan} '  $_prompt_pwd'
colour_addvar '$_prompt_justify'
colour_addvar {grey} '  %D{%a %f %b %Y %H:%M:%S}' {white}

# Configure the left hand prompt
colour_setvar PROMPT {bold} '%# ' {normal}

# Configure our special right hand prompt which is printed when the command has been entered
colour_setvar prompt_right {grey} '%D{%a %f %b %Y %H:%M:%S}' {white}

# Disable the standard ZSH right hand prompt
colour_setvar RPROMPT

# Configure the secondary prompt
colour_setvar PROMPT2 {bold} '%_> ' {normal}

# Configure the prompt used for execution tracing (set -x). Includes timestamp & seconds for
# profiling
colour_setvar PROMPT4
colour_addvar {grey} ': ' {dark_green} '%N' {grey} ':' {dark_yellow} '%i'
colour_addvar {grey} ' %D{%H:%M:%S} {$SECONDS} ; ' {white}
