# ZSH function file
# Part of the SQL Sybase package
# Execute the given SQL command
# For use only by the SQL packages
#
# $Id: _sybase_sql,v 1.6 2001/05/22 16:56:57 mebourne Exp $

if [[ -n $1 ]]
then
  local docompress
  if (( $+compress && compress
        || !$+compress && $+_sql_parameters[compress] && _sql_parameters[compress]))
  then
    docompress='| sqlcompress.pl'
  fi

  {
    if (( $+maxrows ))
    then
      echo "set rowcount $maxrows"
      echo "go"
    elif (( $+_sql_parameters[maxrows] ))
    then
      echo "set rowcount $_sql_parameters[maxrows]"
      echo "go"
    fi

    if (( $+count && !count
          || !$+count && $+_sql_parameters[count] && !_sql_parameters[count] ))
    then
      echo "set nocount on"
      echo "go"
    fi

    echo - "$*"
    echo "go"
  } | eval _sybase_rawsql -w 10000 $docompress
else
  _sybase_rawsql -w ${COLUMNS:-10000}
fi
