# ZSH function file
# Part of the SQL Sybase package
# Execute a block copy command
# For use only by the SQL packages
#

if [[ -z $_sql_parameters[server] ]]
then
  echo "Error: SQL server variable not set" 1>&2
  return 1
fi

local table="$argv[1]" inout="$argv[2]" file="$argv[3]"

# Table needs database as a prefix. Add default if not overridden
if [[ $table != *..* && -n $_sql_parameters[database] ]]
then
  table="$_sql_parameters[database]..$argv[1]"
fi

if (( ${_sql_parameters[kerberos]:-0} ))
then
  kbcp $table $inout $file \
       -S$_sql_parameters[server] \
       -c \
       "${(@)argv[4,-1]}"
else
  if [[ -z $_sql_parameters[user] || -z $_sql_parameters[password] ]]
  then
    echo "Error: SQL user/password variables not set" 1>&2
    return 1
  fi

  bcp $table $inout $file \
      -S$_sql_parameters[server] \
      -U$_sql_parameters[user] \
      -P$_sql_parameters[password] \
      -c \
      "${(@)argv[4,-1]}"
fi
